{
	points: {
		type: "[CurvePoint]",
		init: "[{x: 0.0, y: 0.0}, {x: 1.0, y: 1.0}]",
	},

	_cache: { type: "object", init: "create_cache(1)" },
	_raw_points: "[decimal] :: query_cache(_cache, null, points_along_curve(map(points, if(value.t = null, [value.x,value.y,_get_tangent(index)], [value.x,value.y,value.t])), 256))",

	_get_gradient: "def(CurvePoint a, CurvePoint b) ->decimal
		(b.y - a.y) / (b.x - a.x)
	",

	_get_tangent: "def(int n) ->decimal
	if(grad_a is decimal and grad_b is decimal, (grad_a + grad_b)/2.0, grad_a is decimal, grad_a, grad_b is decimal, grad_b, 0.0)
		where grad_a = if(n > 0, _get_gradient(points[n-1], points[n]), null)
		where grad_b = if(n < size(points)-1, _get_gradient(points[n], points[n+1]))
	",

	builtin_fn: { type: "null|function(decimal) ->decimal" },

	fn: "function(decimal)->decimal :: if(builtin_fn, builtin_fn,
	def(decimal r) ->decimal
	if(r < 0.0 or r > 1.0, 0.0,
		p[bot]*ratio + p[top]*(1.0-ratio)
	)
		where ratio = pos - bot
		where top = ceil(pos)
		where bot = floor(pos)
		where pos = r*decimal(size(p)-1)
		where p = _raw_points
	)
	",

	curves: "[string] :: map(_curves, key)",
	get_curve: "def(string name) ->class easing
		class easing<- _curves[name]
	",

	get: "def(string name) ->function(decimal)->decimal
		get_curve(name).fn
	",

	add_curve: "def(string name, [CurvePoint] curve) ->commands
		_write_curves(_curves + {(name): construct('easing', {points: curve})})
	",

	_revision: { type: "int", default: 1 },

	_curves: "{string -> class easing} ::
	query_cache(global_cache(1), _revision,
	{string -> class easing}<-
	{
		linear: construct('easing', { builtin_fn: lib.math.linear }),
		ease_in_quad: construct('easing', { builtin_fn: lib.math.ease_in_quad }),
		ease_out_quad: construct('easing', { builtin_fn: lib.math.ease_out_quad }),
		ease_in_out_quad: construct('easing', { builtin_fn: lib.math.ease_in_out_quad }),
		ease_in_cubic: construct('easing', { builtin_fn: lib.math.ease_in_cubic }),
		ease_out_cubic: construct('easing', { builtin_fn: lib.math.ease_out_cubic }),
		ease_in_out_cubic: construct('easing', { builtin_fn: lib.math.ease_in_out_cubic }),
		ease_in_quart: construct('easing', { builtin_fn: lib.math.ease_in_quart }),
		ease_out_quart: construct('easing', { builtin_fn: lib.math.ease_out_quart }),
		ease_in_out_quart: construct('easing', { builtin_fn: lib.math.ease_in_out_quart }),
		ease_in_quint: construct('easing', { builtin_fn: lib.math.ease_in_quint }),
		ease_out_quint: construct('easing', { builtin_fn: lib.math.ease_out_quint }),
		ease_in_out_quint: construct('easing', { builtin_fn: lib.math.ease_in_out_quint }),
		ease_in_sine: construct('easing', { builtin_fn: lib.math.ease_in_sine }),
		ease_out_sine: construct('easing', { builtin_fn: lib.math.ease_out_sine }),
		ease_in_out_sine: construct('easing', { builtin_fn: lib.math.ease_in_out_sine }),
		ease_in_expo: construct('easing', { builtin_fn: lib.math.ease_in_expo }),
		ease_out_expo: construct('easing', { builtin_fn: lib.math.ease_out_expo }),
		ease_in_out_expo: construct('easing', { builtin_fn: lib.math.ease_in_out_expo }),
		ease_in_circ: construct('easing', { builtin_fn: lib.math.ease_in_circ }),
		ease_out_circ: construct('easing', { builtin_fn: lib.math.ease_out_circ }),
		ease_in_out_circ: construct('easing', { builtin_fn: lib.math.ease_in_out_circ }),
		ease_in_elastic: construct('easing', { builtin_fn: lib.math.ease_in_elastic }),
		ease_in_out_elastic: construct('easing', { builtin_fn: lib.math.ease_in_out_elastic }),
		ease_in_back: construct('easing', { builtin_fn: lib.math.ease_in_back }),
		ease_out_back: construct('easing', { builtin_fn: lib.math.ease_out_back }),
		ease_in_out_back: construct('easing', { builtin_fn: lib.math.ease_in_out_back }),
		ease_in_bounce: construct('easing', { builtin_fn: lib.math.ease_in_bounce }),
		ease_out_bounce: construct('easing', { builtin_fn: lib.math.ease_out_bounce }),
		ease_in_out_bounce: construct('easing', { builtin_fn: lib.math.ease_in_out_bounce }),
	} +
	fold(
	map(
		{string -> [CurvePoint]}<- (get_document('data/easings.cfg', ['null_on_failure']) or {}),
		{(key): construct('easing', {points: value})}
	), a+b, {})
	)
	",

	_write_curves: "def({string -> class easing} curves) ->commands
	    add(_revision, 1);
		write_document('data/easings.cfg', {string -> [CurvePoint]} <- fold(map(curves, {(key): value.points}), a+b, {}), ['game_dir'])
	",
}
