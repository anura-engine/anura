{
	id: "combo_label",

	is_strict: true,

	properties: {
		_font: { type: "string", init: "lib.font.bold_font" },
		_font_size: { default: 36 },

		_text: { type: "string" },

		_render_key: "[_font, _font_size, bolded, _text]",

		_halign: { type: "string", default: "left" },

		mouseover: { default: false },
		selected: { default: false, set: "if(value != _data, [set(_data, value), fire_event('create')])" },

		bolded: "bool :: grouping != null and (mouseover or selected)",

		grouping: { type: "string|null" },
		index: { type: "int" },

		left_rect: { type: "null|int" },
		right_rect: { type: "null|int" },

		_top_rect_pad: { type: "int", default: 0 },
		_bot_rect_pad: { type: "int", default: 0 },

		text_area: "{width: round_to_even(extents.width), height: round_to_even(font_extents.ascent + font_extents.descent)}
		  where extents = canvas().text_extents(_font, _font_size, _text)
		  where font_extents = canvas().font_extents(_font, _font_size)",

		click_handler: { type: "function(obj combo_label) ->commands" },

		render_label: "def() ->commands
		[
		set(animation, {
			id: 'fbo',
			image: 'fbo',
			x: 0, y: 0, w: area.width, h: area.height,
			scale: 1, frames: 1,
			force_no_alpha: true,
			fbo: query_cache(global_cache(32), _render_key, c.render(area.width, area.height,
			[
				c.save(),
				c.set_operator('SOURCE'),
				c.translate(0, font_extents.ascent),
				c.set_font(_font),
				c.set_font_size(_font_size),
				c.text_path(_text),
				c.set_source_rgba(1, 1, 1, if(bolded, 1.0, 0.6)),
				c.fill(),
				c.restore(),
			]
			)),
		} where c = canvas());

		set(draw_primitives,
			if(left_rect is int, [{
				type: 'rect',
				area: [left_rect + x,y+_top_rect_pad,left_rect + x+1,y2-_bot_rect_pad],
				color: [1,1,1,1],
			}], []) +

			if(right_rect is int, [{
				type: 'rect',
				area: [right_rect + x2,y+_top_rect_pad,right_rect + x2+1,y2-_bot_rect_pad],
				color: [1,1,1,1],
			}], [])
		)
		] where area = text_area
		  where extents = canvas().text_extents(_font, _font_size, _text)
		  where font_extents = canvas().font_extents(_font, _font_size)",
	},

	on_click: "[
		lib.sound.play_sound(me, 'interface/label-click'),

		swallow_mouse_event(),
		click_handler(me),
	]",

	on_paused: "[
		set(mouseover, false),
		fire_event('create')
	]",

	on_mouse_enter: "[
		lib.sound.play_sound(me, 'interface/label-mouseover'),
		set(mouseover, true),
		fire_event('create')
	]",

	on_mouse_leave: "[
		set(mouseover, false),
		fire_event('create')
	]",

	on_create: "[
		render_label(),
	]",

	on_type_updated: "render_label()",
}
