{
	id: "text_entry",
	is_strict: true,
	auto_anchor: true,

	prototype: ["focus_widget"],

	properties: {

		_password: { type: "bool", default: false },

		font_size: { type: "int", init: "lib.gui.py(14)" },

		default_text: {
			type: "string",
			default: "Enter text here",
			set: "if(value != _data, [ _set_dirty(), set(_data, value) ])",
		},
		_default_text_color: { type: "[decimal,decimal,decimal,decimal]", default: [0.5,0.5,0.5,1.0] },
		text: {
			type: "string",
			default: "",
			set: "if(value != _data, [
				set(_data, value),
				if(cursor_pos > size(value), set(cursor_pos, size(value))),
				if(_select_pos != null and _select_pos > size(value), set(_select_pos, null)),
				_set_dirty(),
				if(created and _on_change, ; _on_change())
			])"
		},

		display_text: "string :: if(_password, fold(map(text, '*'), a+b, ''), text)",

		_text_color: { type: "[decimal,decimal,decimal,decimal]", default: [1.0,1.0,1.0,1.0] },

		_highlight_color: { type: "[decimal,decimal,decimal,decimal]", default: [0,0,0.5,1.0] },

		_on_change: { type: "null|function()->commands" },

		_width: { type: "int" },
		_height: { type: "int", init: "lib.gui.py(26)" },

		_scroll_pos: { type: "int", default: 0, set: "if(_data != value, [set(_data, value), _set_dirty()])" },

		cursor_pos: { type: "int", default: 0, set: "if(_data != value,
			[
				set(_data, value),
				if(_select_pos != null, _set_dirty()),
				if(offset > _scroll_pos + _width - _border_left*2,
					set(_scroll_pos, offset - _width + _border_left*2),
				   offset < _scroll_pos + _border_left,
				    set(_scroll_pos, offset)
				),
			] where offset = _get_cursor_offset(value))"
		},
		_select_pos: { type: "null|int", set: "if(_data != value, [set(_data, value), _set_dirty()])" },

		_border_top : { type: "int", init: "lib.gui.py(4)" },
		_border_left : { type: "int", init: "lib.gui.py(4)" },

		_calculate_fragments: "def(cairo_callable c, string str, null|[int,int] selection=null) ->[cairo_text_fragment]
			c.markup_text(q(<font weight='55' size=') + font_size + q('>) +
			if(selection = null, c.escape_text(str),
			 c.escape_text(str[:left]) +
			 q(<font tag='selection'>) +
			 c.escape_text(str[left:right]) +
			 q(</font>) +
			 c.escape_text(str[right:])
			     where left = min(selection[0],selection[1])
			     where right = max(selection[0],selection[1])
			) +
			q(</font>), { width: 10000, no_escaping: true})
		",

		_get_mouse_pos_to_cursor: "def(int raw_pos) ->int
		if(_get_cursor_offset(size(text)) < pos, size(text),
			_get_mouse_pos_to_cursor_internal(pos, 0, size(text))
		)
		where pos = raw_pos + _scroll_pos
		",

		_get_mouse_pos_to_cursor_internal: "def(int pos, int min_pos, int max_pos) ->int
			if(mid = min_pos or mid = max_pos, mid,
			if(offset > pos,
			   _get_mouse_pos_to_cursor_internal(pos, min_pos, mid),
			   _get_mouse_pos_to_cursor_internal(pos, mid, max_pos))
			)
			where offset = _get_cursor_offset(mid)
			where mid = (min_pos + max_pos)/2
		",

		_get_cursor_offset: "def(int pos) ->int
		int(sum(map(fragments, value.x_advance)))
			where fragments = _calculate_fragments(c, display_text[:pos])
			where c = canvas()
		",

		_bg_color: {
			type: "[decimal,decimal,decimal,decimal]",
			default: [0.0, 0.0, 0.0, 1.0],
		},

		render: "def() ->commands [
			set(_dirty, false),
			set(animation, lib.gui.render_frame(c, _width, _height,
			[
				c.rectangle(0, 0, _width, _height),
				c.set_source_color(_bg_color),
				c.fill(),

				c.translate(_border_left - _scroll_pos, _border_top),
				map(fragments, [
					if(value.tag is list and 'selection' in value.tag, [
						c.set_source_color(_highlight_color),
						c.rectangle(value.x, value.y, value.x_advance, value.height),
						c.fill(),
					]),

					c.set_source_color(if(text, _text_color, _default_text_color)),

					c.save(),
					value.path,
					c.fill(),
					c.restore(),
				]),

			])
			) where fragments = _calculate_fragments(c,
			string ::
				if(text, display_text, default_text),
				if(_select_pos != null, [cursor_pos, _select_pos])
			  )
			  where c = canvas()
		]",

		_left_repeat: { type: "int", default: 0 },
		_right_repeat: { type: "int", default: 0 },

		_move_right: "def() ->commands _set_cursor_pos(cursor_pos+1)",
		_move_left: "def() ->commands _set_cursor_pos(cursor_pos-1)",

		on_focus: "def() ->commands _refresh_cursor()",

		_set_cursor_pos: "def(int new_pos) ->commands
		[
		_refresh_cursor(),
		if((_dragging or _key_shift) and _select_pos = null, set(_select_pos, cursor_pos)),
		if(_dragging = false and _key_shift = false, set(_select_pos, null)),
		set(cursor_pos, clamp(new_pos, 0, size(text))),
		]
		",

		_show_cursor: "def(bool shown) ->commands
		if(shown,
			set(draw_primitives, [{
				type: 'line',
				color1: [255,255,255,255],
				color2: [255,255,255,255],
				p1: [x - _scroll_pos + _border_left + pos+1,y + _border_top],
				p2: [x - _scroll_pos + _border_left + pos+1,y + _height - _border_top],
				width: 4,
			} where pos = _get_cursor_offset(cursor_pos)]),
	
			set(draw_primitives, [])
		)
		",

		_cursor_shown_basis: { type: "int", default: 0 },

		_refresh_cursor: "def()->commands set(_cursor_shown_basis, cycle)",

		_backspace_repeat: { type: "int", default: 0 },
		_delete_repeat: { type: "int", default: 0 },

		_backspace: "def() ->commands
		[
		if(_select_pos != null and _select_pos != cursor_pos,
		   _delete(),
			if(cursor_pos > 0, [
				add(cursor_pos, -1),
				set(text, text[:cursor_pos-1] + text[cursor_pos:]),
				_refresh_cursor(),
			]; render()
			)
		)
		]
		",

		_delete_selection: "def() ->commands
		if(_select_pos != null and _select_pos != cursor_pos, _delete())
		",

		_delete: "def() ->commands
			if(_select_pos != null and _select_pos != cursor_pos,
			[
				set(text, text[:left] + text[right:]),
				set(_select_pos, null),
				set(cursor_pos, left),
			]
				where left = min(_select_pos,cursor_pos)
				where right = max(_select_pos,cursor_pos)
			; render(),
					
			cursor_pos < size(text), [
				set(text, text[:cursor_pos] + text[cursor_pos+1:]),
				_refresh_cursor(),
			]; render()
			)
		",

		_set_dirty: "def() ->commands
			set(_dirty, true)
		",

		_dirty: { default: false },

		_dragging: { default: false },

		_recent_clicks: { type: "[int]", default: [] },

		_clipboard_copy: "def() ->commands
			if(_select_pos != null, set_clipboard_text(text[min(cursor_pos,_select_pos):max(cursor_pos,_select_pos)]))
		",

		_clipboard_cut: "def() ->commands
		[
			_clipboard_copy(),
			_delete_selection(),
		]
		",

		_clipboard_paste: "def() ->commands
		_delete_selection();
		_text_input(lib.gui.utf8_to_ascii(get_clipboard_text()))
		",

		_max_size: { type: "null|int" },

		_text_input: "def(string new_text) ->commands
		if(_select_pos != null and _select_pos != cursor_pos, _delete(), set(_select_pos, null));
		[
			set(text, text[:cursor_pos] + new_text_trunc + text[cursor_pos:]),
			add(cursor_pos, size(new_text_trunc)),
	
			_refresh_cursor(),
		] where new_text_trunc = if(_max_size = null or size(text) + size(new_text) <= _max_size, new_text,
		  new_text[:size(new_text) - (size(text) + size(new_text) - _max_size)]
		 )
		; render()
		",
	},

	events: {

	type_updated: "fire_event('create')",

	create: "render()",

	process: "
	proto_event('focus_widget', 'process');
	[
		if(_dirty, render()),

		_show_cursor(has_focus and (cycle - _cursor_shown_basis)%60 < 30),

		if(has_focus, [

		if(_key_control and _key_pressed('x'), _clipboard_cut()),
		if(_key_control and _key_pressed('c'), _clipboard_copy()),
		if(_key_control and _key_pressed('v'), _clipboard_paste()),

		if(_key_home, _set_cursor_pos(0)),
		if(_key_end, _set_cursor_pos(size(text))),

		if(_key_backspace,
		   add(_backspace_repeat, 1); if(_backspace_repeat = 1 or _backspace_repeat > 20 and _backspace_repeat%3 = 0, _backspace()),
		   set(_backspace_repeat, 0)
		),

		if(_key_delete,
		   add(_delete_repeat, 1); if(_delete_repeat = 1 or _delete_repeat > 20 and _delete_repeat%3 = 0, _delete()),
		   set(_delete_repeat, 0)
		),

		if(level.player.ctrl_left,
		   add(_left_repeat, 1); if(_left_repeat = 1 or _left_repeat > 20 and _left_repeat%3 = 0, _move_left()),
		   set(_left_repeat, 0)
		),

		if(level.player.ctrl_right,
		   add(_right_repeat, 1); if(_right_repeat = 1 or _right_repeat > 20 and _right_repeat%3 = 0, _move_right()),
		   set(_right_repeat, 0)
		),

		]),
	]
	",

	text_input: "if(has_focus,
	_text_input(arg.text)

	)",

	click: "
		add(_recent_clicks, [cycle]); if(size(_recent_clicks) > 3, set(_recent_clicks, _recent_clicks[size(_recent_clicks)-3:]));
	
	[
		if(size(_recent_clicks) >= 3 and _recent_clicks[size(_recent_clicks)-3] > cycle-50,
		  //triple click
		  [
		  set(_select_pos, 0),
		  set(cursor_pos, size(text)),
		  set(_recent_clicks, []),
		  ],

		  size(_recent_clicks) >= 2 and _recent_clicks[size(_recent_clicks)-2] > cycle-30,
		  //double click

		  [
		  set(_select_pos, if(space_before = -1, 0, cursor_pos - space_before)),
		  set(cursor_pos, if(space_after = -1, size(text), cursor_pos + space_after)),
		  ]

		  where space_before = index(before, ' ')
		  where space_after = index(after, ' ')
		  where before = reverse(chars[:cursor_pos])
		  where after = chars[cursor_pos:]
		  where chars = split(text, '')
		 ),

	]",

	mouse_down: "[
		set(has_focus, true),
		_set_cursor_pos(_get_mouse_pos_to_cursor(arg.mouse_x - x - _border_left))
	]",

	drag_start: "[
		set(_dragging, true);
		_set_cursor_pos(_get_mouse_pos_to_cursor(arg.mouse_x - x - _border_left))
	]",

	drag: "[
		_set_cursor_pos(_get_mouse_pos_to_cursor(arg.mouse_x - x - _border_left))
	]",

	drag_end: "[
		set(_dragging, false),
	]",
	},
}
