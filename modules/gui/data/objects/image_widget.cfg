{
	id: "image_widget",
	is_strict: true,
	always_active: true,
	mouse_drag_threshold: 5,

	properties: {
		allow_drag: { type: "bool", default: false },
		drag_pos: { type: "[int,int]", default: [0,0] },

		drag_start_handler: { type: "null|function(obj image_widget)->commands" },
		drag_handler: { type: "null|function(obj image_widget)->commands" },
		drag_end_handler: { type: "null|function(obj image_widget)->commands" },

		click_handler: { type: "null|function(obj image_widget)->commands" },
		mouse_enter_handler: { type: "null|function(obj image_widget)->commands" },
		mouse_leave_handler: { type: "null|function(obj image_widget)->commands" },

		zoom: { type: "decimal", default: 0.0 },
		saturation: { type: "decimal", default: 1.0 },
		luminance: { type: "decimal", default: 1.0 },

		hue_target: { type: "decimal", default: 0.0 },
		hue_lerp: { type: "decimal", default: 0.0 },
		hue_shift: { type: "decimal", default: 0.0 },

		image: { type: "string" },
		_base_image_area: { type: "[decimal,decimal,decimal,decimal]", default: [0,0,0,0] },
		image_area: { type: "[decimal,decimal,decimal,decimal]" },

		//set this image_area to trigger redraws when it's set.
		image_area_animated: { variable: false, type: "[decimal,decimal,decimal,decimal]", get: "image_area", set: "[set(image_area, value), set_dirty()]" },

		sprite_adjust: "[decimal,decimal,decimal,decimal] ::
		[
			img_area[0]-_base_image_area[0],
			img_area[1]-_base_image_area[1],
			img_area[2]-_base_image_area[2],
			img_area[3]-_base_image_area[3],
		]
		where img_area = _calculate_image_area()
		",

		wave: { type: "{intensity: decimal, period: decimal, offset: decimal}|null",
		        get: "_data",
				set: "[
					set(_data, value),
					if(value = null, [
						set(shader.uniform_commands.u_wave_intensity, 0.0),
					], [
						set(shader.uniform_commands.u_wave_intensity, value.intensity),
						set(shader.uniform_commands.u_wave_period, value.period),
						set(shader.uniform_commands.u_wave_offset, value.offset),
					])
				] asserting shader != null",
		},

		draw_width: { type: "int" },
		draw_height: { type: "int" },

		_calculate_image_area: "def() ->[decimal,decimal,decimal,decimal]
		  if(src_ratio = dst_ratio,
				image_area,
		     src_ratio < dst_ratio,
			   //e.g. converting 4:3 to 16:10. Cut some off the top
			   //and bottom.
			   [image_area[0], image_area[1] + height_diff/2, image_area[2], image_area[3] - height_diff/2]
			 where height_diff = src_height - desired_src_height
			 where desired_src_height = src_width / dst_ratio,

			  //e.g. converting 16:10 to 4:3. Cut off some of the sides.
              [image_area[0] + width_diff/2, image_area[1], image_area[2] - width_diff/2, image_area[3]]
			 where width_diff = src_width - desired_src_width
			 where desired_src_width = src_height * dst_ratio

		  )
			where dst_ratio = decimal(draw_width)/decimal(draw_height),
			where src_ratio = decimal(src_width)/decimal(src_height)
			where src_width = image_area[2] - image_area[0]
			where src_height = image_area[3] - image_area[1]
		",

		set_dirty: "def() ->commands set(_dirty, true)",
		_dirty: { type: "bool", default: true },
	},

	events: {

	create: "
	[
		set(animation, {
			id: 'image',
			force_no_alpha: true,
			image: 'fbo',
			fbo: query_cache(lib.gui.image_cache, image, get_texture({ image: image, filtering: 'bilinear', mipmaps: 4, surface_flags: ['NO_ALPHA_FILTER', 'NO_CACHE'] })),
			x: xpos,
			y: ypos,
			w: x2pos - xpos - 1,
			h: y2pos - ypos - 1,
			scale: decimal(draw_width) / decimal(x2pos - xpos),
			frames: 1,
		}),

		set(_base_image_area, [xpos, ypos, x2pos, y2pos]),
		set(shader.uniform_commands.u_sprite_dim, [x2pos - xpos, y2pos - ypos]),
	] asserting shader != null
	where xpos = int(img_area[0])
	where ypos = int(img_area[1])
	where x2pos = int(img_area[2])
	where y2pos = int(img_area[3])
	where img_area = _calculate_image_area()
	",

	mouse_enter: "if(mouse_enter_handler, [
		swallow_mouse_event(),
		mouse_enter_handler(me),
	])",

	mouse_leave: "if(mouse_leave_handler, [
		swallow_mouse_event(),
		mouse_leave_handler(me),
	])",

	click: "if(click_handler, [
		swallow_mouse_event(),
		click_handler(me),
	])",

	drag_start: "if(allow_drag, [
		swallow_mouse_event(),
		set(drag_pos, lib.gui.mouse_pos(level)),
	]);

	if(drag_start_handler, drag_start_handler(me))
	",

	drag: "if(allow_drag, [
		swallow_mouse_event(),
		set(drag_pos, mouse_pos),

		add(mid_x, mouse_delta.x),
		add(mid_y, mouse_delta.y),
	]

	where mouse_delta = [mouse_pos.x - drag_pos.x, mouse_pos.y - drag_pos.y]
	where mouse_pos = lib.gui.mouse_pos(level)
	);

	if(drag_handler, drag_handler(me))
	
	",

	drag_end: "if(allow_drag, [
		swallow_mouse_event(),
	]);

	if(drag_end_handler, drag_end_handler(me))
	",

	},

	shader: {
	name: "card_image_render",
	vertex: "
	uniform mat4 u_anura_mvp_matrix;
	uniform vec4 u_anura_sprite_area;
	uniform vec4 u_anura_draw_area;
	uniform vec4 u_property_sprite_adjust;
	uniform vec2 u_sprite_dim;
	attribute vec4 a_anura_vertex;
	attribute vec2 a_anura_texcoord;
	varying vec2 v_texcoord;
	void main()
	{
		float px_width = (u_anura_sprite_area[2] - u_anura_sprite_area[0]) / u_sprite_dim[0];
		float px_height = (u_anura_sprite_area[3] - u_anura_sprite_area[1]) / u_sprite_dim[1];

		v_texcoord = a_anura_texcoord;

		if(v_texcoord[0] < (u_anura_sprite_area[0] + u_anura_sprite_area[2])*0.5) {
			v_texcoord[0] += px_width*u_property_sprite_adjust[0];
		} else {
			v_texcoord[0] += px_width*u_property_sprite_adjust[2];
		}

		if(v_texcoord[1] < (u_anura_sprite_area[1] + u_anura_sprite_area[3])*0.5) {
			v_texcoord[1] += px_height*u_property_sprite_adjust[1];
		} else {
			v_texcoord[1] += px_height*u_property_sprite_adjust[3];
		}

		gl_Position = u_anura_mvp_matrix * a_anura_vertex;
	}
	",

	fragment: "
	vec3 rgb_to_hsv(vec3 c)
	{
		vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
		vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
		vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));

		float d = q.x - min(q.w, q.y);
		float e = 1.0e-10;
		return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
	}

	vec3 hsv_to_rgb(vec3 c)
	{
		vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
		vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
		return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
	}


	uniform sampler2D u_anura_tex_map;
	uniform vec4 u_anura_sprite_area;
	uniform vec2 u_sprite_dim;
	uniform float u_wave_intensity;
	uniform float u_wave_period;
	uniform float u_wave_offset;
	uniform float u_alpha;
	uniform float u_property_saturation;
	uniform float u_property_luminance;
	uniform float u_property_hue_target;
	uniform float u_property_hue_lerp;
	uniform float u_property_hue_shift;
	uniform float u_property_zoom;
	varying vec2 v_texcoord;
	void main()
	{
		vec2 texcoord = v_texcoord;
		if(u_property_zoom != 0.0) {
			vec2 center = vec2((u_anura_sprite_area[0] + u_anura_sprite_area[2])*0.5, (u_anura_sprite_area[1] + u_anura_sprite_area[3])*0.5);
			texcoord = mix(texcoord, center, u_property_zoom);
		}

		if(u_wave_intensity > 0.0) {
			float xpos = (v_texcoord[0] - u_anura_sprite_area[0]) / (u_anura_sprite_area[2] - u_anura_sprite_area[0]);
			float ypos = (v_texcoord[1] - u_anura_sprite_area[1]) / (u_anura_sprite_area[3] - u_anura_sprite_area[1]);

			float distort = sin(xpos*u_wave_period + u_wave_offset)*min(u_wave_intensity, 1.0 - 2.0*abs(xpos - 0.5)) /
			                (u_anura_sprite_area[2] - u_anura_sprite_area[0]);
			texcoord[0] += distort;
		}

		float px_width = (u_anura_sprite_area[2] - u_anura_sprite_area[0]) / u_sprite_dim[0];
		float px_height = (u_anura_sprite_area[3] - u_anura_sprite_area[1]) / u_sprite_dim[1];

		gl_FragColor = texture2D(u_anura_tex_map, texcoord);
		gl_FragColor[3] *= u_alpha;

		if(u_property_saturation != 1.0 || u_property_luminance != 1.0 || u_property_hue_lerp != 0.0 || u_property_hue_shift != 0.0) {
			vec3 hsv = rgb_to_hsv(gl_FragColor.rgb);
			hsv[0] = fract(mix(hsv[0], u_property_hue_target, u_property_hue_lerp) + u_property_hue_shift);
			hsv[1] *= u_property_saturation;
			hsv[2] *= u_property_luminance;
			gl_FragColor.rgb = hsv_to_rgb(hsv);
		}
	}
	",

	draw: "[
		set(uniform_commands.u_alpha, parent.alpha/255.0)
	]
	",

	},
}
