//Additional functions for math.
{


#-------------------------- misc --------------------------#

	//x and y returns y if x is 0, use default(x,y) if 0 is a valid return.
	default: "def(decimal|null input, decimal|null fallback) -> decimal if(input is decimal, input, fallback asserting fallback is decimal)",
	



#-------------------------- number-range functions --------------------------#

	in_range: "def(decimal min, decimal var, decimal max) -> bool not var < min or var > max",

	//Force the 2nd arg to be between the first and third arg, or at least as in-between as possible.
	//The failure mode, 'center it', comes in to play when the minimum number is larger than the maximum.
	//This is especially handy when dealing with variable-width GUIs, when you want something in the middle of a rectangular window with 100px of padding – or just in the center, if the 100px isn't available. While we could assert, it's less than helpful when your program crashes because someone's screen is too small.
	constrain: "overload( //Same function, different signatures.
		def(int min, int var, int max) -> int 
			if(min > max, (min+max)/2, if(min > var, min, if(max < var, max, var))),
		def(decimal min, decimal var, decimal max) -> decimal 
			if(min > max, (min+max)/2, if(min > var, min, if(max < var, max, var)))
		)",

	is_sorted: "def(list x) -> bool x = sort(x)", //Mostly good for testing range like if w<=x<=y. 




#-------------------------- rounding and decimal/int conversion functions --------------------------#
//also including some decimal versions of various integer-based builtins

	//In Anura, occasionally we want symmetrical math that mirrors itself between -1 and 1. The following is true for floor, ceil, and round: f(x) = -f(-x).
	floor: "def(decimal num) -> int int(num)",

	ceil: "def(decimal num) -> int if(int(num) = num, int(num), int(num)+sign(num))",

	round: "def(decimal x) -> int round(x)", //Left this for consistency here after adding round() to the engine.

	//We need a function for doing modulo arithmetic that doesn't round off fractional values
	decimal_mod: "def(decimal val, int modulo) -> decimal mod(val, modulo) + val - floor(val)",

	//The mod operator, %, only works on integers. This function gives the decimal component of a number as a partial workaround.
	tenth: "def(decimal a) -> decimal a-int(a)", //Returns the number to the right of the decimal place. eg, ±2.4→±0.4.

	//round a value to the nearest decimal place:  i.e. round_to_decimal(1.33333, 10.0) gives 1.3
	round_to_decimal_precision: "def(decimal num, decimal magnitude) -> decimal round(num * magnitude) / magnitude",



#-------------------------- easing/interpolation --------------------------#
	interpolate: "overload(
		/* 
		Given a start, a percentage, and an end; return the appropriate in-between value. 
			Examples:
				- interpolate(5, 0.5, 0) would return 2.5.
				- interpolate(level.player, 0.75, [0,0]) would return a point three quarters of the way between the player and the point of origin.
		Given a list of things and a percentage, return the thing (a point or decimal) that is percent of the way along the list. All points are weighted evenly. Points can be anything that lib.standardize.toPoint accepts as input.
			Use: interpolate(list_of_stops, percent)
			For example, between 0.0 and 1.0 for [1,3,1], we'd have the following values returned: 
				[1.0, 1.4, 1.8, 2.2, 2.6, 3.0, 2.6, 2.2, 1.8, 1.4, 1.0]
		*///3 ARITY
		def(decimal begin, decimal percentage, decimal end) -> decimal 
			tween(end, begin, percentage),
		
		def([decimal] begin, decimal percentage, [decimal] end) -> [decimal] 
			[decimal] <- tween(end, begin, percentage) //The base case, where we just tween between the two.
				asserting size(begin) = size(end) 
						  | {begin:begin, end:end},
					  
		(def([decimal] begin, decimal percentage, object|map end) -> map 
			({mid_x:x_, mid_y:y_, 0:x_, 1:y_} where    //Extract and tween.
				x_ = tween(decimal <- end.mid_x, begin[0], percentage), 
				y_ = tween(decimal <- end.mid_y, begin[1], percentage)) 
				asserting end.mid_x != null, 
						  end.mid_y != null, 
						  size(begin)=2 
						  | {begin:begin, end:end}),
					  
		(def(object|map begin, decimal percentage, [decimal] end) -> map 
			({mid_x:x_, mid_y:y_, 0:x_, 1:y_} where 
				x_ = tween(end[0], decimal <- begin.mid_x, percentage), 
				y_ = tween(end[1], decimal <- begin.mid_y, percentage)) 
				asserting begin.mid_x != null, 
						  begin.mid_y != null, 
						  size(end)=2 
						  | {begin:begin, end:end}),
					  
		(def(object|map begin, decimal percentage, object|map end) -> map {
			mid_x:tween(decimal <- end.mid_x, decimal <- begin.mid_x, percentage), 
			mid_y:tween(decimal <- end.mid_y, decimal <- begin.mid_y, percentage)
			} asserting begin.mid_x != null, 
						end.mid_x != null, 
						begin.mid_y != null, 
						end.mid_y != null),
					
		//2 ARITY
		def({decimal -> decimal} targets, decimal step) -> decimal 
			//We'll take the list of points as verticies connected by edges (think fence-posts). Each edge has two verticies, and we'll tween between those.
			(tween(targets[second_key], targets[first_key], (step-first_key)/total_range) where 
				total_range = second_key - first_key where first_key=decimal(min(keys(targets))), 
				second_key = decimal(max(keys(targets))) 
			) asserting size(targets) = 2,
		
		(def([object|map|[decimal]]|[decimal] targets, decimal step) -> decimal|[decimal]|map 
			interpolate(
				object|map|[decimal]|decimal <- targets[chunkIndex], 
				chunkPercent, 
				object|map|[decimal]|decimal <- if(size(targets)!=chunkIndex+1, 
												   targets[chunkIndex+1], 
												   targets[chunkIndex])
			) where chunkIndex = int(expandedPercent), 
					chunkPercent = tenth(expandedPercent)
					where expandedPercent = step*(size(targets)-1)) 
				
	) where tween = overload(
	
		def(decimal list_one, decimal list_two, decimal percent_raw) -> decimal 
			//Percent_raw may be ever so slightly above 1 or below 0, depending on some less-than-percise math elsewhere. For example, the camera controller will ask for percent 1.0005 on its last frame.
			(list_one*(percent) + list_two*(1.0-percent)) where percent = constrain(0, percent_raw, 1),
	
		def([decimal] list_one, [decimal] list_two, decimal percent_raw) -> list 
			//Same as before, but now combine each element of both lists weighted by percent.
			zip(list_one, list_two, 
				a*percent + b*(1.0-percent)
			) where percent = lib.math.constrain(0, percent_raw, 1)
	)",


	//This is a really basic smooth-step function, like the above but faster and without the constraints interpolate has.
	//Takes three decimals; start, stop, and percent of the way between the two.
	smooth_step: "def(decimal a, decimal b, decimal percent) -> decimal
		b*percent + a*(1-percent)",




	//Easeing functions. Takes a percent (a decimal between 0 and 1) and makes it closer to either extreme, depending on where it is.
	sin_in:     "def(decimal percent) -> decimal 1-cos(percent*90)",
	sin_in_out: "def(decimal percent) -> decimal if(percent < 0.5, sin_in(percent*2)/2, sin_out((percent-0.5)*2)/2+0.5)",
	sin_out:    "def(decimal percent) -> decimal sin(percent*90)",
	linear:     "def(decimal percent) -> decimal percent",


#-------------------------- line/rectangle geometry --------------------------#

	length: "overload(
		def(decimal a, decimal b, decimal u, decimal v) -> decimal hypot(a-u, b-v), //c,d? d is for dice, 2d20, and can't be used.
		def([decimal] a, [decimal] b) -> decimal length(a[0], a[1], b[0], b[1]),
		def(custom_obj a, [decimal] b) -> decimal length(a.midpoint_x, a.midpoint_y, b[0], b[1]),
		def([decimal] a, custom_obj b) -> decimal length(a[0], a[1], b.midpoint_x, b.midpoint_y),
		def(custom_obj a, custom_obj b) -> decimal length(a.midpoint_x, a.midpoint_y, b.midpoint_x, b.midpoint_y),
	
		def(decimal u, decimal v) -> decimal length(0,0,u,v),
		def([decimal] b) -> decimal length(0,0, b[0],b[1])
	)",

	rect_intersect: "overload( 
		//Takes two RectXY Rects, or two RectWH Rects and a reference to (for example) lib.standardize.to_rect_xy as the third arg.
		def(Rect a, Rect b) -> bool not
			a[0] > b[2] or a[2] < b[0] or 
			a[1] > b[3] or a[3] < b[1],
		def(Rect a, Rect b, function(Rect)->Rect standardize) -> bool
			rect_intersect(standardize(a), standardize(b))
	)",


#-------------------------- circular/trigonometric geometry --------------------------#

	//Useful for normalizing when adding the degrees of a circle together. Always returns a positive.
	//Example use: We want a value between 0° and 360°, so we can divide by 360.0° and tell how much of a percent around a circle we are. Say, for a dial control.
	//Our equation is 45+90+90+180 degrees. loop returns 45 degrees, so we know we're 12.5% the way around our circle.
	loop: "overload(
		def(decimal a) -> decimal loop(a, 360),
		def(decimal a, decimal lim) -> decimal (lim+a%lim)%lim //this returns an int :|
	)",


	//Calculate the angle between a point of some sort and another point. The four-arg version, accepting x1,y1,x2,y2, is to keep parity with the built-in angle function.
	//This is a convenience function so you don't have to manually separate out the components of your points. It just uses the engine angle function.
	angle: "overload( //this returns an int because of loop
		def(decimal x1, decimal y1, decimal x2, decimal y2) -> decimal loop(angle(x1, y1, x2, y2)), //Normalize the return value to between 0 and 360, because that is the most expected.
		def([decimal] p1, [decimal] p2) -> decimal lib.math.angle(p1[0], p1[1], p2[0], p2[1]),
		def(object|map p1, object|map p2) -> decimal lib.math.angle(
			decimal <- default(decimal|null <- p1.mid_x, decimal|null <- p1.x), 
			decimal <- default(decimal|null <- p1.mid_y, decimal|null <- p1.y), 
			decimal <- default(decimal|null <- p2.mid_x, decimal|null <- p2.x), 
			decimal <- default(decimal|null <- p2.mid_y, decimal|null <- p2.y)),
		def(object|map p1, [decimal] p2) -> decimal lib.math.angle(
			decimal <- default(decimal|null <- p1.mid_x, decimal|null <- p1.x), 
			decimal <- default(decimal|null <- p1.mid_y, decimal|null <- p1.y), p2[0], p2[1]),
		def([decimal] p1, object|map p2) -> decimal lib.math.angle(p1[0], p1[1], 
			decimal <- default(decimal|null <- p2.mid_x, decimal|null <- p2.x), 
			decimal <- default(decimal|null <- p2.mid_y, decimal|null <- p2.y)),
		
		def(decimal a, decimal b) -> decimal lib.math.angle(0,0,a,b),
		def([decimal] p1) -> decimal lib.math.angle(0,0,p1[0],p1[1])
	)",


	//This function could use some love. It was just copy/pasted out of an ancient controller for curved ropes.
	bezier_curve: "def([Point2d] points, decimal percent_raw) -> Point2d
		if(size(points) < 2,
			points[0], //Just return the point, no work to be done.
			if(size(points)=2, 
				[(decimal <- points[0][0]*(1-percent)) + (decimal <- points[1][0]*percent), (decimal <- points[0][1]*(1-percent)) + (decimal <- points[1][1]*percent)], 
				bezier_curve([Point2d] <- map(range(size(points)-1), 'ind', bezier_curve([Point2d] <- points[ind:ind+2], percent)), percent)
		)) where percent = decimal(percent_raw) 
		   asserting size(points)",


	rotate: "overload(
		def(decimal x, decimal y, decimal rotation, Point2d point) -> Point2d
			Point2d <- rotate_rect(int(x), int(y), rotation, map(point, int(value))),
		def(Point2d xy, decimal rotation, Point2d point) -> Point2d
			rotate(xy[0], xy[1], rotation, point),
		def(decimal x, decimal y, decimal rotation, [Point2d] points) -> [Point2d]
			map(points, rotate(x, y, rotation, value)),
		def(Point2d xy, decimal rotation, [Point2d] points) -> [Point2d]
			map(points, rotate(xy[0], xy[1], rotation, value)),
		
		def(decimal rotation, Point2d point) -> Point2d
			rotate(0, 0, rotation, point),
		def(decimal rotation, [Point2d] points) -> [Point2d]
			map(points, rotate(0, 0, rotation, value)),
	)",



#-------------------------- line-of-sight geometry --------------------------#

	/*
	Answers the question, "Can object A see object B?"

	Takes: 1) A level. 2) The custom_obj object A. 3) The hittable or custom_obj object B.
	Returns an object with 
		clear: bool (ie, clear line of sight) = true if can see directly
		saw: First hittable object (or object B) or null if no object seen

	Remarks: Perhaps this should go in a different library? math.cfg shouldn't really hold level-based geometry functions like this.
	*/
	clear_line_of_sight_between: "overload(
		def(level level, custom_obj obj_a, obj hittable obj_b) -> {clear: bool, saw: null | obj hittable}
			{clear: bool, saw: null | obj hittable} <- _cs_step_to_collision(level, _cs_line_of_sight(obj_a, obj_b), obj_a, obj_b),
		def(level level, custom_obj obj_a, custom_obj obj_b) -> {clear: bool, saw: null | custom_obj}
			_cs_step_to_collision(level, _cs_line_of_sight(obj_a, obj_b), obj_a, obj_b)
		)",

	_cs_step_to_collision: "def(level level, [Point] steps, custom_obj obj_a, custom_obj obj_b, int iteration=0) -> {clear: bool, saw: null | custom_obj} //This doesn't work, returning merely a bunch of strings - a list of valid functions?
		base iteration = size(steps): 
			{clear: true, saw: obj_b} //No collisions.
		base solid(level, int(steps[iteration][0]), int(steps[iteration][1]), 2, 2): //Append 'true' to see where is being tested. 
			{clear: false, saw: null} //Level collision.
		base find(filter(level.active_chars, value is obj hittable and value not in [obj_a, obj_b]), 'char', 
				char.solid_rect.x < steps[iteration][0] and steps[iteration][0] < char.solid_rect.x2 and
				char.solid_rect.y < steps[iteration][1] and steps[iteration][1] < char.solid_rect.y2) :
			{clear: false, saw:
			find(filter(level.active_chars, value is obj hittable and value not in [obj_a, obj_b]), 'char', 
				char.solid_rect.x < steps[iteration][0] and steps[iteration][0] < char.solid_rect.x2 and
				char.solid_rect.y < steps[iteration][1] and steps[iteration][1] < char.solid_rect.y2) }
		recursive: _cs_step_to_collision(level, steps, obj_a, obj_b, iteration+1)",

	//Return a list of points on the line of sight between the two objects.
	_cs_line_of_sight: "def(custom_obj obj_a, custom_obj obj_b) -> [Point] 
		map(range(steps_to_fire+1), 'step', [
			(fot[0]*(percent) + gs[0]*(1.0-percent)),
			(fot[1]*(percent) + gs[1]*(1.0-percent))
		] where percent = step/1.0/steps_to_fire
		) where steps_to_fire = _cs_steps_to_fire(obj_a, obj_b)
		where gs = obj_a.mid_xy,
			  fot = obj_b.mid_xy",
	  
	_cs_steps_to_fire: "def(custom_obj obj_a, custom_obj obj_b) -> int round(length(obj_a, obj_b)/10)",




#-------------------------- Functions inspired by or directly translated from iquilezles.org. I wish I understood the math. --------------------------#

	// See http://iquilezles.org/www/articles/functions/functions.htm

	/*
	Takes decimal value, decimal threshhold, decimal replacement value.

	Say you don't want to change a value unless it's too small and screws some of
	your computations up. Then, rather than doing a sharp conditional branch, you
	can blend your value with your threshold, and do it smoothly (say, with a cubic
	polynomial). Set m to be your threshold (anything above m stays unchanged), and 
	n the value things will take when your value is zero.
	*/
	almost_identity: "def(decimal x, decimal m, decimal n) -> decimal
		if(x >= m, 
			decimal(x), 
			((a*t + b)*t^2 + n where
				a = 2.0*n - m,
				b = 2.0*m - 3.0*n,
				t = x/1.0/m)
		)
	",

	//For symmetry, we of course also need...
	identity: "def(any x) -> any x",


	/*
	Great for triggering behaviours or making envelopes for music or animation, and
	for anything that grows fast and then slowly decays. Use k to control the
	streching o the function. Btw, it's maximun, which is 1.0, happens at exactly x
	= 1/k.
	*/
	impulse: "def(decimal k, decimal x) -> decimal
		h * exp(1.0-h) where h = k*x",


	/*
	Takes pulse center-mark, pulse duration, and input centered around the center-mark. Returns a value between 1 and 0.

	Of course you found yourself doing smoothstep(c-w,c,x)-smoothstep(c,c+w,x) very
	often, probably cause you were trying to isolate some features. Then this
	cubicPulse() is your friend. Also, why not, you can use it as a cheap
	replacement for a gaussian.
	*/
	cubic_pulse: "def(decimal c, decimal w, decimal x_a) -> decimal
		if(x_b > w, 
			0.0, 
			1.0 - x_c^2 * (3 - 2*x_c) 
				where x_c = x_b/1.0/w
		) where x_b = decimal(abs(x_a - c))
	",


	/*
	Takes a range around 0..1 and a parabola curve factor of around 1. Outputs 0 when range is 0 or 1, and 1 when range is 0.5.

	A natural attenuation is an exponential of a linearly decaying quantity: yellow*
	curve, exp(-x). A gaussian, is an exponential of a quadratically decaying
	quantity: light green curve, exp(-x²). You can go on increasing powers, and get
	a sharper and sharper smoothstep(), until you get a step() in the limit.

	*Note: See the website for the graph this is referring to.
	*/
	exponential_step: "def(decimal x, decimal k, decimal n) -> decimal exp(-k*x^n)",


	/*
	A nice choice to remap the 0..1 interval into 0..1, such that the corners are
	remaped to 0 and the center to 1. In other words, parabola(0) = parabola(1) = 0,
	and parabola(1/2) = 1.
	*/
	parabola: "def(decimal x, decimal k) -> decimal (4.0*x*(1.0-x))^k",




//And, some constants...
e: 2.718281,
pi: 3.141592,


test: [
	{
		assert: "map(range(11), lib.math.interpolate([1,3,1], value/10.0)) 
			= [1.0, 1.4, 1.8, 2.2, 2.6, 3.0, 2.6, 2.2, 1.8, 1.4, 1.0]"
	},{
		assert: "lib.math.interpolate({mid_x:100, mid_y:50}, 0.40, {mid_x:0, mid_y:-10})
			= {mid_x:60, mid_y:26}"
	},
	
	{
		assert: "lib.math.smooth_step(5, 10, -2) = -5"
	},{
		assert: "lib.math.smooth_step(5, 10, -1) = 0"
	},{
		assert: "lib.math.smooth_step(5, 10, -0.5) = 2.5"
	},{
		assert: "lib.math.smooth_step(5, 10, 0) = 5"
	},{
		assert: "lib.math.smooth_step(5, 10, 0.5) = 7.5"
	},{
		assert: "lib.math.smooth_step(5, 10, 2) = 15"
	},

	{
		assert: "lib.math.loop(360) = 0"
	},{
		assert: "lib.math.loop(360+45) = 45"
	},{
		assert: "lib.math.loop(-360+45) = 45"
	},{
		assert: "lib.math.loop(720+90) = 90"
	},{
		assert: "lib.math.loop(-720+90) = 90"
	},
	
	{
		assert: "lib.math.round(-5.4) = -5"
	},{
		assert: "lib.math.round(-5.5) = -6"
	},{
		assert: "lib.math.round(-5.6) = -6"
	},{
		assert: "lib.math.round( 5.4) =  5"
	},{
		assert: "lib.math.round( 5.5) =  6"
	},{
		assert: "lib.math.round( 5.6) =  6"
	},
	
	{
		assert: "lib.math.floor( 4.9) =  4"
	},{
		assert: "lib.math.floor( 4.2) =  4"
	},{
		assert: "lib.math.floor(-4.9) = -4"
	},{
		assert: "lib.math.floor(-4.2) = -4"
	},
	
	{
		assert: "lib.math.ceil( 4.9) =  5"
	},{
		assert: "lib.math.ceil( 4.2) =  5"
	},{
		assert: "lib.math.ceil(-4.9) = -5"
	},{
		assert: "lib.math.ceil(-4.2) = -5"
	},
	
	{
		assert: "abs(lib.math.length(-20,30, 80,90) - 116.619037) < 0.0001"
	}, {
		assert: "lib.math.length(-20,30, 80,90) = lib.math.length([-20,30], [80,90])", //TODO: Add an object to this library so we can make one to test this function with.
	},

	{
		//Since angle uses floating-point numbers under the hood, we'll not get back an exact answer.
		//Instead, we'll check that the difference between the expected result and the desired result is within a reasonable margin.
		assert: "abs(angle(1,2,3,4) - 45) < 0.0001"
	},{
		assert: "lib.math.angle({x:10, y:5}, [0,5]) - 180 < 0.0001"
	},
	
	{
		assert: "lib.math.rect_intersect([1056, 128, 1172, 244],[1119, 201, 1119, 201])"
	},
	
	{
		assert: "lib.math.almost_identity(0, 2, 1) = 1"
	},{
		assert: "lib.math.almost_identity(1, 2, 1) = 1.25"
	},{
		assert: "lib.math.almost_identity(2, 2, 1) = 2"
	},
	
	{
		assert: "lib.math.impulse(0.25, 4) = 1"
	},{
		assert: "lib.math.impulse(0.20, 5) = 1"
	},{
		assert: "lib.math.impulse(0.25, 0) = 0"
	},{
		assert: "lib.math.is_sorted([-0.001, lib.math.impulse(0.50, 4) - 0.735758, 0.001])"
	},
	
	{
		assert: "lib.math.cubic_pulse(1,1,0) = 0"
	},{
		assert: "lib.math.cubic_pulse(1,1,1) = 1"
	},{
		assert: "lib.math.cubic_pulse(1,1,2) = 0"
	},
],

}
