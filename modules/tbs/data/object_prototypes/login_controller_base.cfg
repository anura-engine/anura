{
	id: "login_controller_base",
	hidden_in_game: true,
	is_strict: true,

	properties: {
		screen_width: "int :: SCREEN_WIDTH",
		screen_height: "int :: SCREEN_HEIGHT",

		use_beta_key: "bool :: false",

		logo: "string :: 'angel-wings.svg'",
		logo_size: "int :: lib.gui.py(150)",
		title: "string :: MODULE_PRETTY_NAME",
		title_size: "int :: lib.gui.py(96)",

		_logo: { type: "null|obj game_icon" },
		_title: { type: "null|obj label" },

		_progress: { type: "null|obj progress_controller" },
		_show_progress: "def(string message)->commands
		if(not _progress,
			spawn('progress_controller', {
				message: message,
			}, [
				set(_progress, child),
			]),
		)
		",

		_remove_progress: "def()->commands
		[
			remove_object(_progress),
			set(_progress, null),
		]
		",

		_removed_widgets: { type: "[custom_obj]", default: [] },

		return: "def() ->commands [
			map(_removed_widgets, add_object(value)),
			map([me] + _widgets, remove_object(value)),
			set(_widgets, []),
		]",

		_do_auto_login: "def(string cookie) ->commands [
			_show_progress('Logging in'),
			set(_client, cl),
			execute(me, tbs_send(cl, {
				type: 'auto_login',
				cookie: cookie,
			})),

			_add_button('Play Offline', 60*3, me._play_offline),
			_add_button('Exit Game', 60*4, me._exit_game),

		] where cl = tbs_client(TBS_SERVER_ADDRESS, TBS_SERVER_PORT, { retry_on_timeout: false })",

		_enter_login_or_register: "def() ->commands [
			map(_widgets, remove_object(value)),
			set(_widgets, []),

			_add_button('Login', log_in_btn_pos, me._enter_login_screen),
			_add_button('Create Account', create_account_btn_pos, me._enter_register_screen),
			_add_button('Play as Guest', play_as_guest_btn_pos, me._do_login_guest),
			if (show_off_line_btn,
				_add_button('Play Offline', off_line_btn_pos, me._play_offline)),
			_add_button('Fullscreen', full_screen_btn_pos, me._toggle_fullscreen, game_preferences().get_bool_preference_value('desktop_fullscreen')),
			_add_button('Exit Game', exit_game_btn_pos, me._exit_game),
			if(_removed_widgets, _add_button('Cancel', cancel_btn_pos, me.return)),

			_add_terms_and_conditions_button(),
		]
		where
			log_in_btn_pos = if (show_off_line_btn, 50 * 0, 60 * 0),
			create_account_btn_pos = if (show_off_line_btn, 50 * 1, 60 * 1),
			play_as_guest_btn_pos = if (show_off_line_btn, 50 * 2, 60 * 2),
			off_line_btn_pos = if (show_off_line_btn, 50 * 3, -1),
			full_screen_btn_pos = if (show_off_line_btn, 50 * 4, 60 * 3),
			exit_game_btn_pos = if (show_off_line_btn, 50 * 5, 60 * 4),
			cancel_btn_pos = if (show_off_line_btn, 50 * 5, 60 * 4)
		where show_off_line_btn = false
		where developer_mode =
			if (MODULE_OPTIONS.devtools is null,
				// The 'tictactoe' module is probably using this. We would want the 'off
				// line' mode to be always there when the newcomer boots the 'tictactoe'
				// module for the first time.
				true,
				// The 'citadel' module or other production-grade modules should be using
				// this, just please do not remove the field from the module options...
				if (MODULE_OPTIONS.devtools is bool,
					// When releasing an update the 'devtools' module option should
					// be turned to 'false' in most release cases.
					MODULE_OPTIONS.devtools,
					// In case no configuration is matching expectations, assume
					// 'release'.
					false))
		",

		_toggle_fullscreen: "def() ->commands [
			game_preferences().set_preference_value('desktop_fullscreen', not game_preferences().get_bool_preference_value('desktop_fullscreen'), [enum persistent]);
			game_preferences().save_preferences();

			if(globals.pending_restart = null,
				set(globals.pending_restart, true),
				set(globals.pending_restart, not globals.pending_restart),
			),
			//   Do not directly restart if the preference is
			// swapped with current mode as a result of the preference
			// change having been requested in a no-restart-allowed
			// moment.
			if(not globals.pending_restart,
			update_and_restart()
			)
		]",

		_enter_login_screen: "def() ->commands [
			map(_widgets, remove_object(value)),
			set(_widgets, []),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.gui.py(200),
				_password: false,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80),
				default_text: 'Username',
				_on_enter: me._do_login,
				has_focus: true,
			}, [
				add(_widgets, [child]),
				set(_username_entry, child),
			]),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.gui.py(200),
				_password: true,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80 + 40),
				default_text: 'Password',
				_on_enter: me._do_login,
			}, [
				add(_widgets, [child]),
				set(_passwd_entry, child),
			]),

			spawn('button_controller', {
				text: 'Remember Me',
				zorder: zorder+1,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80 + 40*2),
				_font_size: lib.gui.px(14),
				button_width: lib.gui.py(200),
				button_height: lib.gui.py(24),
				checked: remember_me(),
				on_click: def
					() -> commands
					store_remember_me_preference(
						not remember_me()),
			}, [
				add(_widgets, [child]),
			]),

			if(_error_message != null,
				spawn('label', {
					mid_x: screen_width/2,
					mid_y: screen_height/2 + lib.gui.py(60 + 40*4),
					_color: [1,0,0,1],
					_text: [_error_message],
					zorder: zorder+1,
					_font_size: lib.gui.py(14),
				}, [
					add(_widgets, [child]),
				])
			),

			_add_button('Login', 60*3, me._do_login),
			_add_button('Cancel', 60*4, me._enter_login_or_register),

			if(_error_message, _add_button('Help Logging In', 60*5, me._help_logging_in)),

			_add_terms_and_conditions_button(),
		]",

		_help_logging_in: "def() ->commands
		set(_error_message, '');
		[
			map(_widgets, remove_object(value)),
			set(_widgets, []),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.gui.py(200),
				_password: false,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80),
				default_text: 'Username',
				_on_enter: me._do_login,
				has_focus: true,
			}, [
				add(_widgets, [child]),
				set(_username_entry, child),
			]),

			_add_button('Reset Password', 60*3, me._do_reset_password),
			_add_button('Cancel', 60*4, me._enter_login_or_register),
		]",

		_guest_username: { type: "null|string" },

		_do_login_guest: "def() ->commands
		if(cookie = null,
		   _do_register_guest(),

			map(_widgets, remove_object(value));
			set(_widgets, []);
		    set(_guest_username, 'Guest');
			_do_auto_login(cookie)
		)
			where cookie = lib.tbs.read_cookie('guest_')
		",

		_do_register_guest: "def() ->commands
		execute(me,

		set(_guest_username, 'Guest' + str(1d100000));
		[
			_show_progress('Registering'),

			map(_widgets, remove_object(value)),
			set(_widgets, []),

			_add_button('Play Offline', 60*3, me._play_offline),
			_add_button('Exit Game', 60*4, me._exit_game),

			set(_registering, true),
			set(_client, cl),
			execute(me, tbs_send(cl, {
				type: 'register',
				user: get_username(),
				passwd: md5(str(1d1000000) + str(1d1000000) + str(1d1000000)),
				email: '',
				remember: true,
			})),
		] where cl = tbs_client(TBS_SERVER_ADDRESS, TBS_SERVER_PORT, { retry_on_timeout: false })
		)
		",

		_do_register: "def() ->commands
		execute(me,
		if(get_confirm_password() != get_password(),
		[
			set(_error_message, 'Passwords do not match');
			_enter_register_screen()
		],
		use_beta_key and regex_match(get_beta_key(), '^[A-Za-z0-9]{5}$') = null,
		[
			set(_error_message, 'must provide a 5-digit beta key');
			_enter_register_screen()
		],

		[
			_show_progress('Registering'),
			set(_registering, true),
			set(_client, cl),
			execute(me, tbs_send(cl, {
				type: 'register',
				user: get_username(),
				passwd: md5(get_password()),
				email: get_email(),
				remember: remember_me(),
			} + if(use_beta_key, {
				beta_key: get_beta_key(),
			}, {}))),
		] where cl = tbs_client(TBS_SERVER_ADDRESS, TBS_SERVER_PORT, { retry_on_timeout: false })

		)
		)",

		_do_login: "def() ->commands
		execute(me,
		[
			_show_progress('Logging in'),
			set(_registering, false),
			set(_client, cl),
			execute(me, tbs_send(cl, {
				type: 'login',
				user: get_username(),
				passwd: md5(get_password()),
				remember: remember_me(),
			})),
		] where cl = tbs_client(TBS_SERVER_ADDRESS, TBS_SERVER_PORT, { retry_on_timeout: false })
		)
		",

		_do_reset_password: "def() ->commands
		execute(me,
		[
			_show_progress('Resetting password'),
			set(_client, cl),
			execute(me, tbs_send(cl, {
				type: 'recover_account',
				user: get_username(),
			})),
		] where cl = tbs_client(TBS_SERVER_ADDRESS, TBS_SERVER_PORT, { retry_on_timeout: false })
		)
		",

		_registering: { type: "bool", default: false },

		_client: { type: "object|null", default: null },

		get_username: "def() ->string
			if(_guest_username != null, _guest_username, _username_entry.text asserting _username_entry != null)
		",

		get_password: "def() ->string
			_passwd_entry.text asserting _passwd_entry != null
		",

		get_confirm_password: "def() ->string
			_confirm_passwd_entry.text asserting _confirm_passwd_entry != null
		",

		get_beta_key: "def() ->string
			_beta_key_entry.text asserting _beta_key_entry != null
		",

		get_email: "def() ->string
			_email_entry.text asserting _email_entry != null
		",

		_username_entry: { type: "null|obj text_entry" },
		_passwd_entry: { type: "null|obj text_entry" },
		_confirm_passwd_entry: { type: "null|obj text_entry" },
		_beta_key_entry: { type: "null|obj text_entry" },
		_email_entry: { type: "null|obj text_entry" },

		retrieve_bool_preference: "def
			(string preference) -> bool
			game_preferences().get_bool_preference_value(
					preference)
		",

		store_bool_preference: "def
			(string preference, bool b) -> commands
			[
				preferences.set_preference_value(
					preference, b, [ enum persistent]);
				preferences.save_preferences()
			] where preferences = game_preferences()
		",

		retrieve_remember_me_preference: "def
			() -> bool
			retrieve_bool_preference('remember_me')
		",

		store_remember_me_preference: "def
			(bool b) -> commands
			[
				store_bool_preference('remember_me', b),
			]
		",

		remember_me: "def
			() -> bool
			retrieve_remember_me_preference()
		",

		_error_message: { type: "null|string" },

		_enter_register_screen: "def() ->commands [
			map(_widgets, remove_object(value)),
			set(_widgets, []),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.gui.py(200),
				_password: false,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80),
				default_text: 'Username',
				text: if(_username_entry, _username_entry.text, ''),
				_on_enter: me._do_register,
				has_focus: true,
			}, [
				add(_widgets, [child]),
				set(_username_entry, child),
			]),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.gui.py(200),
				_password: true,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80 + 35),
				text: if(_passwd_entry, _passwd_entry.text, ''),
				default_text: 'Password',
				_on_enter: me._do_register,
			}, [
				add(_widgets, [child]),
				set(_passwd_entry, child),
			]),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.gui.py(200),
				_password: true,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80 + 70),
				default_text: 'Confirm Password',
				text: if(_confirm_passwd_entry, _confirm_passwd_entry.text, ''),
				_on_enter: me._do_register,
			}, [
				add(_widgets, [child]),
				set(_confirm_passwd_entry, child),
			]),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.gui.py(200),
				_password: false,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80 + 105),
				default_text: 'Email (optional)',
				text: if(_email_entry, _email_entry.text, ''),
			}, [
				add(_widgets, [child]),
				set(_email_entry, child),
			]),

			if(use_beta_key,
			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.gui.py(200),
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80 + 140),
				default_text: 'Beta Key',
				text: if(_beta_key_entry, _beta_key_entry.text, ''),
				_on_enter: me._do_register,
			}, [
				add(_widgets, [child]),
				set(_beta_key_entry, child),
			])
			),

			spawn('button_controller', {
				text: 'Remember Me',
				zorder: zorder+1,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80 + 35*5),
				_font_size: lib.gui.px(14),
				button_width: lib.gui.py(200),
				button_height: lib.gui.py(24),
				checked: true,
				on_click: def() ->commands null,
			}, [
				add(_widgets, [child]),
			]),

			if(_error_message != null,
				spawn('label', {
					mid_x: screen_width/2,
					mid_y: screen_height/2 + lib.gui.py(75 + 35*6),
					_color: [1,0,0,1],
					_text: [_error_message],
					zorder: zorder+1,
					_font_size: lib.gui.py(14),
				}, [
					add(_widgets, [child]),
				])
			),

			_add_button('Create Account', 60*4, me._do_register),
			_add_button('Cancel', 60*5, me._enter_login_or_register),

			_add_terms_and_conditions_button(),

		]",

		_add_button: "def(string text, int pos, function()->commands on_click, null|bool is_checked=null) ->commands
			spawn('button_controller', {
				text: text,
				zorder: zorder+1,
				button_width: lib.gui.py(200),
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.gui.py(80 + pos),
				on_click: on_click,
				checked: is_checked,
			}, [
				add(_widgets, [child]),
			])
		",

		_add_terms_and_conditions_button: "def() ->commands
			spawn('button_controller', {
				text: q(Terms &amp; Conditions),
				zorder: zorder+1,
				button_width: lib.gui.py(200),
				x: lib.gui.py(6),
				y2: screen_height - lib.gui.py(6),
				on_click: show_terms,
				_font_size: lib.gui.py(12),
			}, [
				add(_widgets, [child]),
			])
		",

		show_terms: "def() ->commands [
			spawn('terms_and_conditions', {
			}),
		]",

		_widgets: { type: "[custom_obj]", default: [] },

		_play_offline: "def() ->commands [
			set(lib.tbs.local_username, 'Guest'); lib.chat.init_chat(),
			map(_widgets, remove_object(value)),
			remove_object(me),
			add_object(playable),
		
		] where playable = object_playable('title_controller', 0, 0, 1, {
			_removed_widgets: _removed_widgets,
			game_logo: _logo,
			game_title: _title,
			_mm_client_state: construct('mm_client_state', {
				username: 'Player',
				session_id: -1,
				account_info_version: -1,
				account_info: _offline_account,
			}),
		})
		",

		_offline_account: "AccountDetails<- { chat_channels: {} }",

		_exit_game: "def() ->commands quit_to_desktop()",

		_complete_login: "def(int session_id, string username, string|null cookie, AccountDetails info, int info_version, int timestamp) ->commands [
			set(lib.tbs.server_time_delta, timestamp - time().unix),
			lib.tbs.write_cookie(cookie, if(_guest_username != null, 'guest_', '')),
			set(lib.tbs.local_username, username); lib.chat.init_chat(),
			map(_widgets, remove_object(value)),
			remove_object(me),
			add_object(playable),
		] where playable = object_playable('title_controller', 0, 0, 1, {
			_removed_widgets: _removed_widgets,
			game_logo: _logo,
			game_title: _title,
			_mm_client_state: construct('mm_client_state', {
				client: tbs_client(TBS_SERVER_ADDRESS, TBS_SERVER_PORT, {
					session: session_id,
					id: 'matchmake',
				}),
				username: username,
				session_id: session_id,
				account_info_version: info_version,
				account_info: info,
			}),
		})",

		setup_dimensions: "def() ->commands
			set(level.dimensions, [0,0,level.camera_position[2], level.camera_position[3]])
		",

		impersonate: "def(string username, string pass) ->commands [
			set(_client, cl),
			execute(me, tbs_send(cl, {
				type: 'login',
				user: username,
				passwd: md5('blah'),
				remember: false,
				impersonate: pass,
			})),
			_show_progress('Logging in'),
		
		] where cl = tbs_client(TBS_SERVER_ADDRESS, TBS_SERVER_PORT, { retry_on_timeout: false })",

		_allow_auto_login: "bool :: true",
	},

	events: {
		create: "setup_dimensions(); [
			[[remove_object(b), add(_removed_widgets, [b])] | b <- level.chars, b is obj button_controller],

			spawn('game_icon', level.dimensions[2]/2, lib.gui.py(170), {
				icon: logo,
				size: logo_size,
			}, [
				set(_logo, child),
			]),

			spawn('label', level.dimensions[2]/2, lib.gui.py(300), {
				_text: [title],
				_font_size: title_size,
				_halign: 'center',
			}, [
				set(_title, child),
			]),

			if(cookie and _allow_auto_login,
				_do_auto_login(cookie),
				_enter_login_or_register()
			)

			where cookie = lib.tbs.read_cookie()

		]",

		process: "if(_client, tbs_process(_client))",

		message_received: "
		_remove_progress();
		log(['MESSAGE', arg.message]);
		switch(message.type,
			'login_fail', [
				set(_error_message, string<- message.message);
				if(_registering, _enter_register_screen(), _enter_login_screen()),
			],
			'message', if(message.message is string, [
				set(_error_message, string<- message.message);
				if(_registering, _enter_register_screen(), _enter_login_screen()),
			]),
			'error', if(message.message is string, [
				set(_error_message, string<- message.message);
				if(_registering, _enter_register_screen(), _enter_login_screen()),
			]),
			'auto_login_fail', [
				_enter_login_screen()
			],
			'login_success', [
				_complete_login(int<- message.session_id, string<- message.username, string|null<- message.cookie, AccountDetails<- message.info, int<- message.info_version, int<- message.timestamp),
			],
			'registration_success', [
				_complete_login(int<- message.session_id, string<- message.username, string|null<- message.cookie, AccountDetails<- message.info, int<- message.info_version, int<- message.timestamp),
			],
		)
		where message = map<- arg.message",

		settings_menu: "set(level.show_builtin_settings_dialog, false)",
	},
}
