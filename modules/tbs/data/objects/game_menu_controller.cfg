{
	id: "game_menu_controller",
	is_strict: true,
	zorder: 20000,
	hidden_in_game: true,

	properties: {
		level_width: "level.dimensions[2]",
		level_height: "level.dimensions[3]",

		buttons: { type: "[{pad: int|null, text: string, callback: function()->commands}]" },

		text_width: { type: "int", init: "lib.gui.py(260)" },

		_blur: { type: "bool", default: true },

		_text_area: { type: "null|obj text_area" },
		_version_area: { type: "null|obj text_area" },
		_blur_controller: { type: "null|obj blur_controller" },
		_buttons: { type: "[obj button_controller]", default: [] },

		_exclude_from_pause: { type: "[custom_obj]", default: [] },
		_paused: { type: "[custom_obj]", default: [] },

		show_dialog: "def() ->commands
		execute(me,
		[
			lib.sound.play_sound(me, 'interface/enter-escape-menu'),
			[remove_object(c) | c <- spawned_children],

			[
				set(_paused, need_pause),
				map(need_pause, set(value.paused, true)),
			] where need_pause = filter(level.chars, value.paused = false and (value not in _exclude_from_pause)),
			add_object(me),

			spawn('text_area', 0, 0, {
				mid_x: level_width/2,
				mid_y: level_height/2,
				bg_color: [0.3, 0.3, 0.3, 1.0],
				bg_border_color: [0.40, 0.42, 0.45, 1.0],
				bg_border_width: lib.gui.dpy(3.0),
				border: lib.gui.py(20),
				border_sides: lib.gui.py(50),
				border_bot: size(buttons)*lib.gui.py(60) + sum([int] :: map(buttons, value.pad or 0)) + lib.gui.py(40),
				_text: q(<font size='24'>Game Menu</font>),
				text_width: text_width,
				zorder: zorder,
			}, [
				set(_text_area, child),
			]);

			spawn('text_area', 0, 0, {
				mid_x: level_width/2,
				mid_y: (obj text_area<- _text_area).y2 - lib.gui.py(20),
				bg_color: [0.3, 0.3, 0.3, 1.0],
				_text: q(<font size='12'>Argentum Age ) + str(MODULE_VERSION) + q(</font>),
				text_width: text_width,
				zorder: zorder+1,
			}, [
				set(_version_area, child),
			]);

			if(_blur,
				spawn('blur_controller', {
					blur_amount: 0.0,
					end_zorder: me.zorder-10,
				}, [
					set(me._blur_controller, child),
					animate(child, {
						blur_amount: 2.0,
					}, {
						duration: 10,
					}),
				])
			);

			if(_text_area != null, [
				map(buttons,
					spawn('button_controller', {
						mid_x: level_width/2,
						y: level_height/2 + lib.gui.py(30) - size(buttons)*lib.gui.py(60)/2 - sum(map(buttons, value.pad or 0))/2 + index*lib.gui.py(60) + sum(map(buttons[:index+1], value.pad or 0)),
						button_width: lib.gui.py(212),
						zorder: zorder+10,
						text: value.text,
						on_click: (def()->commands [remove_object(me); value.callback()]),
						_font_size: lib.gui.py(18),
					}, [
						add(_buttons, [child]),
					])
				)
				where left_side = level_width/2 - text_width/4
				where button_spacing = (text_width/2)/(max(1, size(buttons)-1))
			])
		]
		)
		",
	},

	on_create: "show_dialog()",

	on_being_removed: "[
		map(_paused, set(value.paused, false)),
		map(_buttons, remove_object(value)),
		remove_object(_text_area),
		remove_object(_version_area),
		if(_blur_controller, execute(_blur_controller,
		animate(_blur_controller, {
			blur_amount: 0.0,
		}, {
			duration: 10,
			on_complete: remove_object(_blur_controller)
		})))
	]",
	
	on_type_updated: "show_dialog()",
}
