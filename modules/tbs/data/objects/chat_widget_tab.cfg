{
	id: "chat_widget_tab",
	is_strict: true,

	properties: {
		close_handler: { type: "null|function()->commands" },
		_close_button: { type: "null|obj svg_button" },

		_chat_widget: { type: "obj chat_widget" },

		_font_size: { type: "int", init: "lib.citadel.py(14)" },

		_hpad: "int ::
			if(diff > 0,
				max(_min_hpad, _max_hpad - diff/2),
				_max_hpad
			)
			where diff = _desired_width - max_width
		",

		_max_hpad: { type: "int", init: "lib.citadel.py(16)", },
		_min_hpad: { type: "int", init: "lib.citadel.py(8)", },

		_indent: { type: "int", init: "lib.citadel.py(12)" },

		text: { type: "string" },

		_cropped_text: "string ::
		if(available >= extents.width, text,
				text[:3] + '...'
		)
		where available = _width - _hpad
		where extents = c.text_extents(lib.font.regular_font, _font_size, text)
		where c = canvas()
		",

		layout_width: "int :: _width - _indent",
		layout_height: "int :: _height",

		max_width: {
			type: "int",
			set: "if(value != _data, [
				set(_data, value),
				_set_dirty(),
			])
			",
		},

		_width: "int :: max(8, min(max_width, _desired_width))",

		_desired_width: "int ::
		int(extents.width) + _max_hpad*2
		where extents = c.text_extents(lib.font.regular_font, _font_size, text)
		where c = canvas()",

		_text_width: "int :: query_cache(global_cache(32), [_font_size, text], int(c.text_extents(lib.font.regular_font, _font_size, text) where c = canvas()))",

		_height: { type: "int", init: "lib.citadel.py(20)", },

		_dirty: { type: "bool", default: true },
		_set_dirty: "def()->commands set(_dirty, true)",

		mouseover: { type: "bool", default: false, change: "_set_dirty()" },
		selected: { type: "bool", default: false, change: "_set_dirty()" },
		highlight: { type: "bool", default: false, change: "_set_dirty()" },
		first_tab: { type: "bool", default: false, change: "_set_dirty()" },
		show_right_indent: { type: "bool", default: true, change: "_set_dirty()" },

		render: "def() ->commands
		[
		set(_dirty, false),
		set(animation,
			lib.citadel.render_frame(c, _width, _height,
			[
				c.set_operator('SOURCE'),

				if(selected,
					c.set_source_rgba(0.1,0.1,0.1,0.8),
				   mouseover,
					c.set_source_rgba(0,0,0,1.0),
					c.set_source_rgba(0,0,0,0.8)
				),

				c.move_to(if(selected or first_tab, 0, _indent), _height),
				c.line_to(0, 0),
				c.line_to(_width - _indent, 0),
				c.line_to(_width - if(show_right_indent, 0, _indent), _height),
				c.line_to(if(selected or first_tab, 0, _indent), _height),

				c.fill(),

				c.new_path(),

				c.move_to(if(selected or first_tab, 0, _indent), _height),
				c.line_to(0, 0),
				c.line_to(_width - _indent, 0),
				c.line_to(_width - if(show_right_indent, 0, _indent), _height),

				c.set_line_width(1.0),
				c.set_source_rgba(0.22,0.25,0.29,1.0),
				c.stroke(),

				c.translate(_width/2.0 - fragments_width/2.0, _height*0.1),
				map(fragments, [
					c.save(),
					if(highlight,
						c.set_source_rgba(0.6, 0.6, 1.0, 1.0),
					   selected or mouseover,
					    c.set_source_rgba(1,1,1,1),
					    c.set_source_rgba(0.9,0.9,0.9,1)
					),
					value.path,
					c.fill(),
					c.restore(),
				]),
			])

			where fragments_width = sum(map(fragments, value.x_advance))
			where fragments = c.markup_text(q(<font weight='45' size=') + _font_size + q('>) + c.escape_text(_cropped_text) + q(</font>), { width: 10000 })
			where c = canvas()
		)
		]
		",
	},

	on_type_updated: "_set_dirty()",

	on_create: "render()",
	on_process: "if(_dirty, render())",

	on_mouse_enter: "[
		if(_close_button, [remove_object(_close_button), set(_close_button, null)]),
		if(close_handler != null,
			spawn('svg_button', {
				icon: 'cancel.svg',
				zorder: zorder+10,
				mid_x: x2 - lib.citadel.py(12),
				mid_y: mid_y - lib.citadel.py(6),
				mouseover_color: [255,0,0],
				mouseoff_color: [196,0,0],
				size: lib.citadel.py(16),
				click_handler: close_handler,
				use_absolute_screen_coordinates: true,
			}, [
				set(_close_button, child),
			])
		),

		set(mouseover, true)
	]",
	on_mouse_leave: "[
		if(_close_button, [remove_object(_close_button), set(_close_button, null)]),
		set(mouseover, false)
	]",

	on_mouse_down: "if(_close_button = null or _close_button.mouseover = false, _chat_widget.tab_clicked(me))",

	on_being_removed: "[remove_object(_close_button), set(_close_button, null)]",
}
